(ns browser.script-scheduler-test
  (:require [browser.script-scheduler :as scheduler]
            [browser.script-engine :as script-engine]
            [browser.session :as session]
            [clojure.test :refer [deftest is]]
            [kotoba.wasm.host :as host]))

(deftest scheduler-starts-synchronous-engine-into-session-atom
  (let [engine {:engine/id "sync"}
        session-atom (atom (session/new-session
                            {:host (host/recording-host)
                             :engine-factory (fn [_] engine)}))
        task (scheduler/start! session-atom)]
    (is (= :ready (:script-scheduler/status task)))
    (is (= :ready (get-in @session-atom [:browser.session/script-engine
                                         :script-engine/status])))
    (is (= engine (get-in @session-atom [:browser.session/script-engine
                                         :script-engine/engine])))
    (is (= [:script-engine/start :script-engine/ready]
           (mapv :event (:browser.session/history @session-atom))))))

(deftest scheduler-aborts-pending-engine-start
  (let [start (session/begin-script-engine-start!
               (session/new-session
                {:host (host/recording-host)
                 :engine-factory (fn [_] :external-pending)}))
        session-atom (atom (:session start))]
    (scheduler/abort! session-atom :navigation/abort)
    (is (= :empty (get-in @session-atom [:browser.session/script-engine
                                         :script-engine/status])))
    (is (= :script-engine/abort
           (-> @session-atom :browser.session/history last :event)))
    (is (= :navigation/abort
           (-> @session-atom :browser.session/history last :reason)))))

(deftest scheduler-passes-abort-signal-to-engine-factory-and-aborts-it
  (let [seen-signal (atom nil)
        start (session/begin-script-engine-start!
               (session/new-session
                {:host (host/recording-host)
                 :engine-factory (fn [session]
                                   (reset! seen-signal (:browser.session/abort-signal session))
                                   :external-pending)}))
        session-atom (atom (:session start))]
    (is @seen-signal)
    (is (not (script-engine/aborted? @seen-signal)))
    (scheduler/abort! session-atom :navigation/new-page)
    (is (script-engine/aborted? @seen-signal))
    (is (= :navigation/new-page (:reason @@seen-signal)))))
