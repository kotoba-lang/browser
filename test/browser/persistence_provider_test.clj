(ns browser.persistence-provider-test
  (:require [browser.account :as account]
            [browser.audit :as audit]
            [browser.chrome :as chrome]
            [browser.persistence :as persistence]
            [browser.persistence-provider :as provider]
            [browser.profile :as profile]
            [browser.storage :as storage]
            [clojure.test :refer [deftest is]]))

(defn- state []
  (let [acct (account/new-account {:id "local"})
        p (profile/new-profile {:id "default" :account acct})]
    {:profile p
     :store (storage/put-value (storage/empty-store) p "kotoba://shell" :k "v")
     :audit (audit/append-event (audit/empty-log)
                                (audit/page-commit-event {:url "kotoba://shell"
                                                          :op-count 1
                                                          :profile-id "default"}))
     :chrome (chrome/new-tab (chrome/empty-chrome) "kotoba://shell")}))

(deftest memory-provider-saves-snapshot-and-datoms
  (let [p (provider/memory-provider)
        snap (provider/save-browser-state! p (state))]
    (is (= snap (provider/load-snapshot! p)))
    (is (seq (:snapshot/datoms snap)))))

(deftest edn-file-provider-persists-snapshot
  (let [tmp (java.io.File/createTempFile "browser-snapshot" ".edn")
        _ (.deleteOnExit tmp)
        p (provider/edn-file-provider (.getPath tmp))
        snap (provider/save-browser-state! p (state))
        loaded (provider/load-snapshot! p)]
    (is (= (:snapshot/datoms snap) (:snapshot/datoms loaded)))
    (is (= persistence/current-version (:snapshot/version loaded)))
    (is (:snapshot/chrome loaded))))

(deftest providers-load-migrated-legacy-snapshots
  (let [tmp (java.io.File/createTempFile "browser-legacy-snapshot" ".edn")
        _ (.deleteOnExit tmp)
        _ (spit tmp (pr-str {:snapshot/store {:storage/data {}}
                             :snapshot/history [{:event :page/commit
                                                 :url "kotoba://legacy"}]}))
        p (provider/edn-file-provider (.getPath tmp))
        loaded (provider/load-snapshot! p)]
    (is (= persistence/current-version (:snapshot/version loaded)))
    (is (= [{:event :page/commit :url "kotoba://legacy"}]
           (get-in loaded [:snapshot/session :history])))))
