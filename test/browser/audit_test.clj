(ns browser.audit-test
  (:require [browser.audit :as audit]
            [clojure.test :refer [deftest is]]))

(deftest audit-events-are-datom-shaped-and-replayable
  (let [a (-> (audit/empty-log)
              (audit/append-event (audit/page-commit-event {:url "kotoba://docs"
                                                            :op-count 7
                                                            :profile-id "default"}))
              (audit/append-event (audit/input-event {:event-type :text/input
                                                      :action-count 1
                                                      :profile-id "default"})))]
    (is (= [[:db/add "browser.audit/e1" :audit/event :page/commit]
            [:db/add "browser.audit/e1" :url "kotoba://docs"]
            [:db/add "browser.audit/e1" :op-count 7]
            [:db/add "browser.audit/e1" :profile/id "default"]]
           (take 4 (:audit/datoms a))))
    (is (= {:page/commit 1 :input/reduce 1}
           (audit/replay-summary a)))))

(deftest permission-and-storage-events-share-audit-shape
  (let [a (audit/append-events
           (audit/empty-log)
           [(audit/permission-event {:permission/decision :deny
                                     :origin "https://example.com"
                                     :capability :input/event
                                     :profile/id "default"
                                     :reason :permission/not-granted})
            (audit/storage-event {:op :storage/put
                                  :key ["profile:default" "kotoba://shell" :session]
                                  :profile-id "default"
                                  :origin "kotoba://shell"})])]
    (is (= {:permission/decision 1 :storage/put 1}
           (audit/replay-summary a)))))

(deftest quickjs-events-are-auditable
  (let [a (audit/append-event
           (audit/empty-log)
           (audit/quickjs-event {:call :js/evaluate
                                 :url "kotoba://app.js"
                                 :request-count 2
                                 :result :ok
                                 :profile-id "default"}))]
    (is (= {:quickjs/call 1} (audit/replay-summary a)))
    (is (some #(= [:db/add "browser.audit/e1" :quickjs/call :js/evaluate] %)
              (:audit/datoms a)))))
