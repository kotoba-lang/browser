(ns browser.storage-test
  (:require [browser.account :as account]
            [browser.audit :as audit]
            [browser.profile :as profile]
            [browser.storage :as storage]
            [clojure.test :refer [deftest is]]))

(deftest storage-is-partitioned-by-profile-and-origin
  (let [acct (account/new-account {:id "local"})
        personal (profile/new-profile {:id "personal" :account acct})
        work (profile/new-profile {:id "work" :account acct})
        store (-> (storage/empty-store)
                  (storage/put-value personal "https://example.com/a" :token "p")
                  (storage/put-value work "https://example.com/a" :token "w")
                  (storage/put-value work "https://other.example/a" :token "other"))]
    (is (= "p" (storage/get-value store personal "https://example.com/b" :token)))
    (is (= "w" (storage/get-value store work "https://example.com/b" :token)))
    (is (= {:token "w"} (storage/origin-entries store work "https://example.com/c")))
    (is (= 3 (count (:storage/log store))))))

(deftest storage-delete-removes-partitioned-value
  (let [acct (account/new-account {:id "local"})
        p (profile/new-profile {:id "default" :account acct})
        store (-> (storage/empty-store)
                  (storage/put-value p "kotoba://shell" :session "s")
                  (storage/delete-value p "kotoba://shell/apps" :session))]
    (is (nil? (storage/get-value store p "kotoba://shell" :session)))
    (is (= :storage/delete (-> store :storage/log last :op)))))

(deftest storage-log-can-be-promoted-to-audit-events
  (let [acct (account/new-account {:id "local"})
        p (profile/new-profile {:id "default" :account acct})
        store (storage/put-value (storage/empty-store) p "kotoba://shell" :session "s")
        a (audit/append-events (audit/empty-log)
                               (map audit/storage-event (:storage/log store)))]
    (is (= {:storage/put 1} (audit/replay-summary a)))))
